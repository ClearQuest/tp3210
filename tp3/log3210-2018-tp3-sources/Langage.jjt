options {
    JAVA_UNICODE_ESCAPE = true;
    STATIC = false;
    MULTI = true;
    VISITOR = true;
}

PARSER_BEGIN(Parser)

package analyzer.ast;

public class Parser
{
    public static ASTProgram parseTree(java.io.InputStream input) throws ParseException
    {
        Parser exo = new Parser(input);
        return exo.Program();
    }
}

PARSER_END(Parser)


//
// ANALYSE LEXICALE
//

// Sauter les blancs
SKIP :
{
    < ( " " | "\t" | "\n" | "\r" )+	>
}

// Commentaires
MORE :
{
    "//" : IN_SINGLE_LINE_COMMENT |
    <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT |
    "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
    <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
    <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
    <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
    < ~[] >
}

// Mots-clés
TOKEN :
{
    <IF: "if" > |
    <ELSE: "else" > |
    <WHILE: "while" > |
    <FOR: "for" > |
    <IN: "in" > |
    <DO: "do" > |
    <TYPE: <STR> | <INT> | <FLOAT> | <BOOL> > |
    < #STR: "string"> |
    < #INT: "int"> |
    < #FLOAT: "float" > |
    < #BOOL: "bool"> |
    <PRINT: "print" > |
    <INPUT: "input" >
}

// Opérateurs
TOKEN :
{
    // Opérateurs d'assignation

    < EQUAL: "=" > |
    < REQUAL: "=." > |
    < AEQUAL: "=*" > |
    < PLUSEQUAL: "+=" > |
    < MINUSEQUAL: "-=" > |
    < POWEQUAL: "^=" > |
    < DIVEQUAL: "/=" > |
    < FOISEQUAL: "*=" > |

    // Opérateurs de comparaison
    <COMPARE: <LESS> | <LESSEQUAL> | <GREAT> | <GREATEQUAL> | <DIFF> | <EQUALEQUAL> > |
    < #LESS: "<" > |
    < #LESSEQUAL: "<=" > |
    < #GREAT: ">" > |
    < #GREATEQUAL: ">=" > |
    < #DIFF: "!=" > |
    < #EQUALEQUAL: "==" > |

    // Opérateurs logiques
    <AND: "&&"  > |
    <OR:  "||"  > |
    <NOT: "not" > |

    // Opérateurs arithmétiques
    <PLUS:  "+" > |
    <MINUS: "-" > |
    <TIMES: "*" > |
    <DIV:   "/" > |

    <RPLUS:  "+." > |
    <RMINUS: "-." > |
    <RTIMES: "*." > |
    <RDIV:   "/." > |

    <CONT:  "|" > |
    <HASH: "#" > |
    <INV: "~" > |

    // Signes de ponctuation
    < LPAREN: "(" > |
    < RPAREN: ")" > |
    < LACC: "{" > |
    < RACC: "}" > |
    < LCRO: "[" > |
    < RCRO: "]" > |
    < COLON: ":" > |
    < SEMICOLON: ";" > |
    < COMMA: "," >
}

// Identificateurs et nombres
TOKEN :
{
    < IDENTIFIER: <LETTER> ( <LETTER> | <DIGIT> )* > |
    < #LETTER: ["a"-"z","A"-"Z","_"] > |
    < #DIGIT: ["0"-"9"] > |
    < INTEGER: ["1"-"9"] (["0"-"9"])* | "0" > |
    // Caution! Allows numbers such as "0000.23"
    < REAL: (<INTEGER>)+ "." (["0"-"9"])+ (<EXPONENT>)? |
            (<INTEGER>)+ <EXPONENT> > |
    < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ > |
    < STRING: "\"" ( <LETTER> | <DIGIT> )* "\"" >
}


//
// ANALYSE SYNTAXIQUE
//

ASTProgram Program() : {}
{

    Block() { return jjtThis; }
}

void Block() : {}
{
     ( Stmt() )*
}

void Stmt() : {}
{   LOOKAHEAD(2) ForStmt() |
    LOOKAHEAD(2) IntAssignStmt() <SEMICOLON> |
    LOOKAHEAD(2) FloatAssignStmt() <SEMICOLON> |
    LOOKAHEAD(2) ArrayAssignStmt() <SEMICOLON>
}



void IntAssignStmt() :{}
{
    (
        Identifier()  <EQUAL>

    ) IntAddExpr()
}


void FloatAssignStmt() :{}
{
    (
         (LOOKAHEAD(2) ArrayAccess() | LOOKAHEAD(2)Identifier())
         <REQUAL>

    ) FloatAddExpr()
}

void ArrayAssignStmt() :{}
{
    (
        Identifier() <AEQUAL>

    ) ArrayContExpr()
}



void ForStmt() : {}
{
    <FOR> Identifier() <IN> ArrayContExpr() <DO> <LACC> Block() <RACC>
}


void Expr() :{}
{
LOOKAHEAD(2) IntAddExpr()|
LOOKAHEAD(2) FloatAddExpr()|
ArrayContExpr()
}

void IntAddExpr() :
{
    Token t;
}
{
    IntMultExpr()
    (
        (t = <PLUS> | t = <MINUS>) IntMultExpr()
        {
            jjtThis.addOp(t.image);
        }
    )*
}

void IntMultExpr() :
{
    Token t;
}
{
    IntUnaryOpExpr()
    (
        (t = <TIMES> | t = <DIV>) IntUnaryOpExpr()
        {
            jjtThis.addOp(t.image);
        }
    )*
}


void IntUnaryOpExpr() :
{
    Token t;
}
{
    (
        (t = <MINUS>)
        {
            jjtThis.addOp(t.image);
        }
    )*
    IntBasicExpr()
}

void IntBasicExpr() : {}
{
    Identifier() |
    IntValue() |
    <LPAREN> IntAddExpr() <RPAREN>
}


void FloatAddExpr() :
{
    Token t;
}
{
    FloatMultExpr()
    (
        (t = <RPLUS> | t = <RMINUS>) FloatMultExpr()
        {
            jjtThis.addOp(t.image);
        }
    )*
}

void FloatMultExpr() :
{
    Token t;
}
{
    FloatUnaryOpExpr()
    (
        (t = <RTIMES> | t = <RDIV>) FloatUnaryOpExpr()
        {
            jjtThis.addOp(t.image);
        }
    )*
}


void FloatUnaryOpExpr() :
{
Token t;
}
{
    (
        (t= <RMINUS>)
        {
            jjtThis.addOp(t.image);
        }
    )*
    FloatBasicExpr()
}


void FloatBasicExpr() : {}
{
    LOOKAHEAD(2) FctStmt() |
    LOOKAHEAD(2) Identifier() |
    RealValue() |
    LOOKAHEAD(2) <LPAREN> FloatAddExpr() <RPAREN>|
    LOOKAHEAD(2) ArrayAccess()
}


void ArrayContExpr() :{}
{
    ArrayRepExpr()
    (
        (<CONT> ) ArrayRepExpr()
    )*
}

void ArrayRepExpr() :{}
{
    (LOOKAHEAD(2) IntAddExpr()

        <HASH>)* ArrayInvExpr()


}


void ArrayInvExpr() :
{
Token t;
}
{
    (
        (t=<INV>)
        {
            jjtThis.addOp(t.image);
        }
    )*
    ArrayBasicExpr()
}

void ArrayBasicExpr() : {}
{
    Identifier() |
    ArrayValue() |
    <LPAREN> ArrayContExpr() <RPAREN>
}

void ArrayAccess() : {}
{
ArrayBasicExpr() <LCRO> IntAddExpr() <RCRO>
}


void FctStmt() : {}
{
    Identifier() <LPAREN> Expr() [ "," Expr() ] <RPAREN>
}


void Identifier() :
{
    Token t;
}
{
    t = <IDENTIFIER> { jjtThis.setValue(t.image); }
}

void IntValue() :
{
    Token t;
}
{
    t = <INTEGER> { jjtThis.setValue(Integer.parseInt(t.image)); }
}

void RealValue() :
{
    Token t;
}
{
    t = <REAL> { jjtThis.setValue(Double.parseDouble(t.image)); }
}

void ArrayValue() :{}
{
    <LCRO> [FloatAddExpr(){jjtThis.addSize();} (<COMMA> FloatAddExpr(){jjtThis.addSize();})*] <RCRO>
}